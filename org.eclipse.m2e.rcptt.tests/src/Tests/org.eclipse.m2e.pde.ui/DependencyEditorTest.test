--- RCPTT testcase ---
Format-Version: 1.0
Contexts: _mo5J8IfaEe2N1bfX2b9TRQ
Element-Name: DependencyEditorTest
Element-Type: testcase
Element-Version: 3.0
External-Reference: 
Id: _WA75MIfaEe2N1bfX2b9TRQ
Runtime-Version: 2.5.4.202210020716
Save-Time: 2/3/23, 7:35 PM
Tags: org.eclipse.m2e.pde.ui
Testcase-Type: ecl
Verifications: _7el9kIfGEe2DibE_7T1Sqw

------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac
Content-Type: text/ecl
Entry-Name: .content

with [get-view "Project Explorer" | get-tree] {
    select "target-platform" | double-click
    select "target-platform/target-platform.target" | double-click
}

with [get-editor "target-platform.target" | get-section Locations | get-tree] {
    get-item -path "Maven Central.*" | click
    select "Maven Central.*" | double-click
}

with [get-window "Maven Artifact Target Entry - Maven Central"] {
    // Check the initial button enablement. All buttons except Undo/Redo should be enabled. 
    get-button Add | get-property enablement | equals true | verify-true
    get-button Remove | get-property enablement | equals true | verify-true
    get-button Update | get-property enablement | equals true | verify-true
    get-button Undo | get-property enablement | equals false | verify-true
    get-button Redo | get-property enablement | equals false | verify-true
    
    // #################################################################### //
    //                                                                      //
    // [I] Check behavior of the Add button with and without the clipboard  //
    //                                                                      //
    // #################################################################### //
    
    to-clipboard -input ""

    // Click add -> Create blank dependency
    get-button Add | click -metaKeys ""
    
    // Invalid GAV -> Update & Finish button should be disabled
    get-button Update | get-property enablement | equals false | verify-true
    get-button Finish | get-property enablement | equals false | verify-true
    
    // Check error messages
    get-editbox -after [get-label "Edit Maven Dependency"] | get-property text 
        | equals "The target location contains one or more incomplete Maven dependencies.\n"
        + " Each dependency requires a group id, artifact id, version and type." | verify-true
    
    get-label "" -after [get-label "Edit Maven Dependency"] 
        | get-property "image.path" | equals "org.eclipse.jface/icons/full/message_error.png" | verify-true
    
    with [get-table | get-item -path "<required>"] {
        get-property "columns[0]" | equals "<required>" | verify-true
        get-property "columns[1]" | equals "<required>" | verify-true
        get-property "columns[2]" | equals "<required>" | verify-true
    }

    // Fill the row with meaningful data...
    with [get-window "Maven Artifact Target Entry - Maven Central" | get-table] {
        get-property "getItems().TableItem[19].getData().getClassifier()" | equals "" | verify-true
        get-property "getItems().TableItem[19].getData().getType()" | equals jar | verify-true
        get-property "getItems().TableItem[19].getData().getVersion()" | equals "" | verify-true
        get-property "getItems().TableItem[19].getData().getGroupId()" | equals "" | verify-true
        get-property "getItems().TableItem[19].getData().getArtifactId()" | equals "" | verify-true
        // Check selection
        get-property "getSelectionCount()" | equals 1 | verify-true
        get-property "getSelection().TableItem[0].getData().getKey()" | equals "::jar:" | verify-true
        // Set GAV to org.apache.commons:commons-lang3:3.12.0
        select "<required>" | activate-cell-edit -type MouseClickSelection
        get-editbox | set-text "org.apache.commons"
        select "<required>" | apply-cell-edit -deactivate
        select "org.apache.commons" | activate-cell-edit -column 1 -type MouseClickSelection
        get-editbox | set-text "commons-lang3"
        select "org.apache.commons" | apply-cell-edit -deactivate
        select "org.apache.commons" | activate-cell-edit -column 2 -type MouseClickSelection
        get-editbox | set-text "3.12.0"
        apply-cell-edit -deactivate
        select "org.apache.commons" | activate-cell-edit -column 3 -type MouseClickSelection
    }
    
    //Valid GAV -> Update & Finish button should now be enabled again
    get-button Update | get-property enablement | equals true | verify-true
    get-button Finish | get-property enablement | equals true | verify-true
    
    to-clipboard -input "<dependency>\
                            <groupId>org.eclipse.platform</groupId>\
                            <artifactId>org.eclipse.core.runtime</artifactId>\
                            <version>3.26.100</version>\
                         </dependency>"
    
    // Click add -> Copy dependency from clipboard
    get-button Add | click -metaKeys ""

    with [get-window "Maven Artifact Target Entry - Maven Central" | get-table] {
        get-property "getItems().TableItem[20].getData().getClassifier()" | equals "" | verify-true
        get-property "getItems().TableItem[20].getData().getType()" | equals jar | verify-true
        get-property "getItems().TableItem[20].getData().getVersion()" | equals "3.26.100" | verify-true
        get-property "getItems().TableItem[20].getData().getGroupId()" | equals "org.eclipse.platform" | verify-true
        get-property "getItems().TableItem[20].getData().getArtifactId()" | equals "org.eclipse.core.runtime"
            | verify-true
        // Check selection
        get-property "getSelectionCount()" | equals 1 | verify-true
        get-property "getSelection().TableItem[0].getData().getKey()" 
            | equals "org.eclipse.platform:org.eclipse.core.runtime:jar:3.26.100" | verify-true
    }
    
    // Check whether you can edit the remaining columns directly. I.e. whether SWT.FULL_SELECTION is enabled.
    with [get-table] {
        select "org.eclipse.jdt" | activate-cell-edit -type MouseClickSelection
        get-editbox | set-text newGroupId
        select "org.apache.commons" | apply-cell-edit -deactivate
        select newGroupId | activate-cell-edit -column 1 -type MouseClickSelection
        get-editbox | set-text newArtifactId
        select "jakarta.xml.bind" | apply-cell-edit -deactivate
        select "jakarta.xml.bind" | activate-cell-edit -column 2 -type MouseClickSelection
        get-editbox | set-text newVersion
        apply-cell-edit -deactivate
    }
    
    // ############################################################################# //
    //                                                                               //
    // [II] Check behavior of the Update button with one more more selected elements //
    //                                                                               //
    // ############################################################################# //

    // Select org.jetbrains.kotlin:kotlin-stdlib-common -> Click Update
    with [get-table] {
        select "org.jetbrains.kotlin" | activate-cell-edit -type MouseClickSelection
        get-editbox | click-text 1 21
        cancel-cell-edit
        deactivate-cell-edit
    }

    get-button Update | click -metaKeys ""

    with [get-table] {
        get-property "getItems().TableItem[12].getData().getVersion()" | equals "1.7.22" | verify-false
    }

    // Select org.jetbrains.kotlin:kotlin-stdlib-jdk7 to org.jetbrains.kotlin:kotlin-stdlib -> Click Update
    with [get-table] {
        select [get-item -path "org.jetbrains.kotlin" -index 1] | activate-cell-edit -type MouseClickSelection
        get-editbox | click-text 1 21
        cancel-cell-edit
        deactivate-cell-edit
        select [get-item -path "org.jetbrains.kotlin" -index 3] [get-item -path "org.jetbrains.kotlin" 
            -index 2] [get-item -path "org.jetbrains.kotlin" -index 1]
    }

    get-button Update | click -metaKeys ""

    with [get-table] {
        get-property "getItems().TableItem[13].getData().getVersion()" | equals "1.7.22" | verify-false
        get-property "getItems().TableItem[14].getData().getVersion()" | equals "1.7.22" | verify-false
        get-property "getItems().TableItem[15].getData().getVersion()" | equals "1.7.22" | verify-false
    }
    
    // ########################################################################## //
    //                                                                            //
    // [III] Check behavior of the Remove button on one or more selected elements //
    //                                                                            //
    // ########################################################################## //
    
    // Selected and remove com.fasterxml.jackson.core:jackson-annotations to com.fasterxml.jackson.core:jackson-databind
    with [get-table] {
        select "com.fasterxml.jackson.core" | activate-cell-edit -type MouseClickSelection
        get-editbox | click-text 1 27
        cancel-cell-edit
        deactivate-cell-edit
        select [get-item -path "com.fasterxml.jackson.core" -index 2] [get-item -path "com.fasterxml.jackson.core" 
            -index 1] "com.fasterxml.jackson.core"
    }
    
    get-button Remove | click -metaKeys ""
    
    // The selected elements should no longer be in the table, with com.github.ben-manes.caffeine:caffeine being the first element
    with [get-table] {
        get-property "getSelection().length" | equals 0 
        get-property "getItemCount()" | equals 18 | verify-true
        get-property "getItems().TableItem[0].getText()" | equals "com.github.ben-manes.caffeine" | verify-true
    }
    
    get-button Undo | click -metaKeys ""
    
    // The artifacts com.fasterxml.jackson.core:jackson-annotations to com.fasterxml.jackson.core:jackson-databind are back and selected
    with [get-table] {
        get-property "getItemCount()" | equals 21 | verify-true
        get-property "getSelection().length" | equals 3 | verify-true
        get-property "getSelection().TableItem[0].getData().getKey()" 
            | equals "com.fasterxml.jackson.core:jackson-annotations:jar:2.14.1" | verify-true
        get-property "getSelection().TableItem[1].getData().getKey()" 
            | equals "com.fasterxml.jackson.core:jackson-core:jar:2.14.1" | verify-true
        get-property "getSelection().TableItem[2].getData().getKey()" 
            | equals "com.fasterxml.jackson.core:jackson-databind:jar:2.14.1" | verify-true
    }
    
    // Select and remove com.squareup.okio:okio-jvm
    with [get-table] {
        select "com.squareup.okio" | activate-cell-edit -type MouseClickSelection
        get-editbox | click-text 1 18
        cancel-cell-edit
        deactivate-cell-edit
    }
    
    get-button Remove | click -metaKeys ""
    
    // The artifact jakarta.activation:jakarta.activation-api should now be selected, because it now at the same position as the removed element
    with [get-table] {
        get-property "getSelection().length" | equals 1 | verify-true
        get-property "getSelection().TableItem[0].getData().getKey()" 
            | equals "jakarta.activation:jakarta.activation-api:jar:1.2.2" | verify-true
    }
    
    // ############################################ //
    //                                              //
    // [IV] Check behavior when sorting by columns  //
    //                                              //
    // ############################################ //
    
    with [get-table] {
        // Sort by Version -> jakarta.inject:jakarta.inject-api should be first element
        get-column-header Version | click
        get-property "getItems().TableItem[0].getData().getKey()" | equals "jakarta.inject:jakarta.inject-api:jar:1.0.5" 
            | verify-true
        // Sort by Artifact Id -> com.github.ben-manes.caffeine:caffeine should be the first element
        get-column-header "Artifact Id" | click
        get-property "getItems().TableItem[0].getData().getKey()" 
            | equals "com.github.ben-manes.caffeine:caffeine:jar:3.1.2" | verify-true
        // Sort by Group Id -> com.fasterxml.jackson.core:jackson-annotations should be the first element
        get-column-header "Group Id" | click
        get-property "getItems().TableItem[0].getData().getKey()" 
            | equals "com.fasterxml.jackson.core:jackson-annotations:jar:2.14.1" | verify-true
    }
    
    // Check whether dependencies are inserted into the correct position when the table is sorted
    to-clipboard -input "<dependency>\
                            <groupId>com.fasterxml.jackson.core</groupId>\
                            <artifactId>jackson-databind</artifactId>\
                            <version>2.14.0</version>\
                         </dependency>"
    
    get-button Add | click -metaKeys ""
    
    with [get-table] {
        get-property enablement | equals true | verify-true
        get-property "getItems().TableItem[2].getData().getKey()" 
            | equals "com.fasterxml.jackson.core:jackson-databind:jar:2.14.0" | verify-true
    }
    
    // ################################## //
    //                                    //
    // [V] Check multi-selection behavior //
    //                                    //
    // ################################## //
    
    // Make sure items don't get lost when selecting more than one element
    with [get-table] {
        // Forward selection
        select "com.fasterxml.jackson.core" | activate-cell-edit -type MouseClickSelection
        get-editbox | click-text 1 27
        cancel-cell-edit
        deactivate-cell-edit
        select "com.github.ben-manes.caffeine" [get-item -path "com.fasterxml.jackson.core" -index 3] [get-item 
            -path "com.fasterxml.jackson.core" -index 2] [get-item -path "com.fasterxml.jackson.core" 
            -index 1] "com.fasterxml.jackson.core"
            
        get-property "selection[0].path[0]" | equals "com.fasterxml.jackson.core" | verify-true
        get-property "selection[1].path[0]" | equals "com.fasterxml.jackson.core%1%" | verify-true
        get-property "selection[2].path[0]" | equals "com.fasterxml.jackson.core%2%" | verify-true
        get-property "selection[3].path[0]" | equals "com.fasterxml.jackson.core%3%" | verify-true
        get-property "selection[4].path[0]" | equals "com.github.ben-manes.caffeine" | verify-true
        
        // Backward selection
        select [get-item -path "org.slf4j" -index 2] | activate-cell-edit -type MouseClickSelection
        get-editbox | click-text 1 10
        cancel-cell-edit
        deactivate-cell-edit
        select [get-item -path "org.slf4j" -index 2] [get-item -path "org.slf4j" -index 1] "org.slf4j" [get-item 
            -path "org.jetbrains.kotlin" -index 3] [get-item -path "org.jetbrains.kotlin" -index 2]
        
        get-property "selection[0].path[0]" | equals "org.jetbrains.kotlin%2%" | verify-true
        get-property "selection[1].path[0]" | equals "org.jetbrains.kotlin%3%" | verify-true
        get-property "selection[2].path[0]" | equals "org.slf4j" | verify-true
        get-property "selection[3].path[0]" | equals "org.slf4j%1%" | verify-true
        get-property "selection[4].path[0]" | equals "org.slf4j%2%" | verify-true
    }
}
------=_.content-0a7243a0-75d3-3d5f-9791-539de0e5b7ac--
